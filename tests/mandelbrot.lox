// Mandelbrot set

// == Setup

var ITERATIONS = 128;

var MIN_REAL = -2;
var MAX_REAL = 0.56;
var MIN_IMAG = -1.28;
var MAX_IMAG = 1.28;

var ROWS = 40;
var COLS = 80;

// == Impl

// looping on the complex plane

var d_real = (MAX_REAL - MIN_REAL) / COLS;
var d_imag = (MAX_IMAG - MIN_IMAG) / ROWS;

var imag = MIN_IMAG;
while ( imag < MAX_IMAG ) {
    var row = "";

    var real = MIN_REAL;
    while ( real < MAX_REAL ) {

        // implementation of the recursion relation
        var xr = real;
        var xi = imag;
        var iter = 0;

        while ( iter < ITERATIONS && (xr * xr + xi * xi) < 4 ) {
            var new_xr = xr * xr - xi * xi + real;
            var new_xi = 2 * xr * xi + imag;
            xr = new_xr;
            xi = new_xi;

            iter = iter + 1;
        };

        if ( iter == ITERATIONS) {
            row = row + "@";
        } else if ( iter >= 64 ) {
            row = row + "%";
        } else if ( iter >= 32 ) {
            row = row + "#";
        } else if ( iter >= 16 ) {
            row = row + "*";
        } else if ( iter >= 8 ) {
            row = row + "+";
        } else if ( iter >= 4 ) {
            row = row + "=";
        } else if ( iter >= 2 ) {
            row = row + "-";
        } else if ( iter >= 1 ) {
            row = row + ":";
        } else {
            row = row + ".";
        };

        real = real + d_real;
    };

    print row;

    imag = imag + d_imag;
};